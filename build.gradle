/*
 * =================================================================================================================
 * Configures the dependencies for the build script itself.
 */
buildscript {
    repositories { mavenCentral() }
}

/*
 * =================================================================================================================
 * subprojects
 * Base configuration for all projects.
 */
allprojects {
    apply plugin: 'eclipse'
}

/*
 * =================================================================================================================
 * subprojects
 * Base configuration for all projects.
 */
subprojects {

    apply plugin: "java"
    apply plugin: 'maven'
    apply from:   '../emma.gradle'

    version = '2.0-MILESTONE-1-SNAPSHOT'
    group = 'com.nudroid'
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    configurations { compileOnly }

    repositories {
        mavenCentral()
        maven {
            url 'http://repo.springsource.org/libs-release'
            url 'http://repo.springsource.org/libs-milestone'
        }
    }

    dependencies {
        testCompile 'org.testng:testng:6.8.5'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

    test { useTestNG() }

    sourceSets.main.compileClasspath += configurations.compileOnly

    eclipse.classpath.plusConfigurations += [configurations.compileOnly]
    
    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    artifacts {
        archives sourceJar
        archives javadocJar
    }
}

/*
 * =================================================================================================================
 * nudroid-provider-interceptors
 * Configuration for nudroid-provider-interceptors
 */
project(':nudroid-annotations') {
}

/*
 * =================================================================================================================
 * nudroid-provider-interceptors
 * Configuration for nudroid-provider-interceptors
 */
project(':nudroid-providers-core') {
    dependencies {
        compile project(':nudroid-annotations')
        compileOnly 'com.google.android:android:2.2.1'
    }
}

/*
 * =================================================================================================================
 * nudroid-annotations-processor
 * Configuration for nudroid-annotations-processor.
 */
project(':nudroid-annotations-processor') {

    dependencies {
        compile project(':nudroid-annotations')
        compile project(':nudroid-providers-core')
        compileOnly 'com.google.guava:guava:15.0'
        compileOnly 'org.apache.velocity:velocity:1.7'
        compileOnly 'com.google.android:android:2.2.1'
    }
}

/*
 * =================================================================================================================
 * nudroid-annotations-processor-all
 * Configuration for nudroid-annotations-processor-all.
 */
project(':nudroid-annotations-processor-all') {

    dependencies {
        compile project(':nudroid-annotations')
        compile project(':nudroid-providers-core')
        compileOnly project(':nudroid-annotations-processor')
        compileOnly 'org.apache.velocity:velocity:1.7'
        compileOnly 'com.google.guava:guava:15.0'
    }

    jar {
        from {
            configurations.compileOnly.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
}