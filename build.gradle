/*
 * Copyright (c) 2014 Nurun Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */



/*
 * =================================================================================================================
 * Configures the dependencies for the build script itself.
 */
buildscript {
    repositories { mavenCentral() }
}

/*
 * =================================================================================================================
 * subprojects
 * Base configuration for all projects.
 */
allprojects {
    apply plugin: 'eclipse'
}

/*
 * =================================================================================================================
 * subprojects
 * Base configuration for all projects.
 */
subprojects {

    apply plugin: "java"
    apply plugin: 'maven'
    apply from:   '../emma.gradle'

    version = '2.0-MILESTONE-2-SNAPSHOT'
    group = 'com.nudroid'
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    configurations { compileOnly }

    repositories {
        mavenCentral()
        maven {
            url 'http://repo.springsource.org/libs-release'
            url 'http://repo.springsource.org/libs-milestone'
            url 'http://artifactory.mtl.nurun.com/artifactory/android-snapshot-local'
            url 'http://artifactory.mtl.nurun.com/artifactory/android-release-local'
        }
    }

    dependencies {
        testCompile 'org.testng:testng:6.8.5'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

    test { useTestNG() }

    sourceSets.main.compileClasspath += configurations.compileOnly
    sourceSets.test.compileClasspath += configurations.compileOnly
    
    javadoc  {
        classpath += configurations.compileOnly
    } 

    eclipse.classpath.plusConfigurations += [configurations.compileOnly]
    
    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    artifacts {
        archives sourceJar
        archives javadocJar
    }
    
    uploadArchives { 
        repositories.mavenDeployer { 
            repository(url: "http://artifactory.mtl.nurun.com/artifactory/android-release-local") {    
                authentication(userName: "pubandroid", password: "androidpub") 
                releases(updatePolicy: 'always')
                snapshots(updatePolicy: 'never')   
            }
            snapshotRepository(url: "http://artifactory.mtl.nurun.com/artifactory/android-snapshot-local") {    
                authentication(userName: "pubandroid", password: "androidpub") 
                releases(updatePolicy: 'never')
                snapshots(updatePolicy: 'always')   
            }
        }     
    }
}

/*
 * =================================================================================================================
 * nudroid-providers-core
 * Configuration for nudroid-providers-core
 */
project(':nudroid-providers-core') {
    dependencies {
        compileOnly 'com.google.android:android:2.2.1'
    }
}

/*
 * =================================================================================================================
 * nudroid-annotations
 * Configuration for nudroid-annotations
 */
project(':nudroid-annotations') {
    dependencies {
        compile project(':nudroid-providers-core')
    }
}

/*
 * =================================================================================================================
 * nudroid-annotations-processor
 * Configuration for nudroid-annotations-processor.
 */
project(':nudroid-annotations-processor') {

    dependencies {
        compile project(':nudroid-annotations')
        compile project(':nudroid-providers-core')
        compileOnly 'com.google.guava:guava:15.0'
        compileOnly 'org.antlr:ST4:4.0.7'
    }
}

/*
 * =================================================================================================================
 * nudroid-annotations-processor-all
 * Configuration for nudroid-annotations-processor-all.
 */
project(':nudroid-annotations-processor-all') {

    dependencies {
        compile project(':nudroid-annotations')
        compile project(':nudroid-providers-core')
        compile 'com.google.android:android:2.2.1'
        compileOnly project(':nudroid-annotations-processor')
        compileOnly 'com.google.guava:guava:15.0'
        compileOnly 'org.antlr:ST4:4.0.7'
    }

    jar {
        from {
            configurations.compileOnly.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}